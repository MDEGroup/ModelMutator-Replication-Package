generate 2 mutants
in "data/out/"
from "data/model/"
metamodel "/SecurityPoliciesExample/data/model/SecurityPolicy.ecore"

with blocks {
	rtt {
		t1 = select one RuleType
		t2 = select one RuleType where {self <> t1 and parameters = t1->parameters}
		modify one Rule	where {type = t1} with {type = t2}
		create PolicyType with {name="class-control-rtt"}[1..5]
	}
	ppr {
		p1 = select one Parameter
		p2 = select one Parameter where {type = p1->type and self <> p1}
		r = select one Rule where {parameters = p1}
		modify r with {parameters -= p1, parameters += p2}
		create PolicyType with {name="class-control-ppr"}[1..5]
	}
	rer {
		remove one Rule
		create PolicyType with {name="class-control-rer"}[1..5]
	}
	ppd {
		r = select one Rule where {parameters->children <> null}
		p = select one Parameter in r->parameters where {children <> null}
		c = select one Parameter in closure(p->children)
		modify r with {parameters -= p, parameters += c}
		create PolicyType with {name="class-control-ppd"}[1..5]
	}
	anr {
		t = select one RuleType
		setp = select all Parameter where {type in t->parameters}
		p = select sample from setp with distinct {type}
		create Rule with {type = t, parameters = p}
		create PolicyType with {name="class-control-anr"}[1..5]
	}
}